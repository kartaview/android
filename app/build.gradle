apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.protobuf'
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url 'https://maven.fabric.io/public' }
    maven {
        url "http://share.skobbler.com/android/maven/"
    }
}
def VERSION_CODE = 73
def VERSION_NAME = "2.0.8"

def SUPPORT_VERSION = "25.3.1"
def PLAY_SERVICES_VERSION = "10.2.1"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        versionCode VERSION_CODE
        versionName VERSION_NAME
        applicationId "org.openstreetcam"
        minSdkVersion 16
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }
    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            ndk {
                abiFilters = []
                abiFilters.addAll(['armeabi', 'armeabi-v7a', 'x86'])
            }
        }
        debug {
            versionNameSuffix '-dev'
            debuggable true
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            ndk {
                abiFilters = []
                abiFilters.addAll(['armeabi', 'armeabi-v7a', 'x86'])
            }
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    dexOptions {
        javaMaxHeapSize "1g"
    }
    lintOptions {
        abortOnError false
        ignoreWarnings true
        quiet true
    }
    aaptOptions {
        useNewCruncher false
    }
    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir 'src/main/proto'
                // In addition to the default '**/*.proto' (use with caution).
                // Using an extension other than 'proto' is NOT recommended,
                // because when proto files are published along with class files, we can
                // only tell the type of a file from its extension.
                include '**/*.protodevel'
            }
            aidl.srcDirs = ['src/main/java']
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/jniLibs'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/CERT.RSA'
        exclude 'META-INF'
    }
}
protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            generatedFilesBaseDir = "generated-proto"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/main/protogen"
}
crashlytics {
    enableNdk true
    androidNdkOut 'build/intermediates/transforms/mergeJniLibs/release/folders/2000/1f/main/lib'
    androidNdkLibsOut 'build/intermediates/transforms/stripDebugSymbol/release/folders/2000/1f/main/lib' //path for my jni libraries
}



task appDebug(dependsOn: [':app:assembleDebug'])
task appRelease(dependsOn: [':app:assembleRelease'])


dependencies {
    compile fileTree(include: 'environment3.jar', dir: 'libs')

    //AARs of the modules down below
//    compile('com.facebook.network.connectionclass:connectivity-osc:1.0.0')
//    compile('com.telenav.ffmpeg:ffmpeg:1.0.3')
//    compile('uk.co.senab.photoview:photoview-osc:1.0.0')
//    compile('com.telenav.streetview.scalablevideoview:scalablevideoview-osc:1.0.0')
//    compile('com.android.volley:volley-osc:1.0.0')
//    compile('org.greenrobot.eventbus:eventbus-osc:1.0.0')
//    compile('com.matthewtamlin.dotindicator:dotindicator-osc:1.0.0')

    //MODULES used for development
    compile project(':eventbus')
    compile project(':photoview')
    compile project(':volley')
    compile project(':connectivity')
    compile project(':scalablevideoview')
    compile project(':ffmpeg')
    compile project(':sdk')
    compile project(':dotindicator')

    compile "com.android.support:support-v4:$SUPPORT_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_VERSION"
    compile "com.android.support:percent:$SUPPORT_VERSION"
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:customtabs:$SUPPORT_VERSION"
    compile "com.android.support:animated-vector-drawable:$SUPPORT_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_VERSION"
    compile "com.google.android.gms:play-services-location:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-auth:$PLAY_SERVICES_VERSION"
    compile "com.google.firebase:firebase-messaging:$PLAY_SERVICES_VERSION"
    compile "com.google.firebase:firebase-auth:$PLAY_SERVICES_VERSION"
    compile 'com.google.api-client:google-api-client-android:1.22.0'
    compile 'com.google.protobuf:protobuf-lite:3.0.0'
    compile 'com.facebook.android:facebook-android-sdk:4.19.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.paolorotolo:appintro:4.1.0'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    compile('org.apache.httpcomponents:httpmime:4.3') { exclude module: "httpclient" }
    compile('oauth.signpost:signpost-commonshttp4:1.2.1.2') { exclude module: "httpcore" exclude module: "commons-codec" }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') { transitive = true }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.5@aar') { transitive = true }

    //TESTS
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_VERSION"
    androidTestCompile "com.android.support:support-v4:$SUPPORT_VERSION"
    androidTestCompile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    androidTestCompile "com.android.support:design:$SUPPORT_VERSION"
    androidTestCompile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
}